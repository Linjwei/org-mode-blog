#+TITLE: Exploring Clojure Macros
#+DESCRIPTION: Exploring Clojure Macros based on some of my code and macros from the Clojure core...
#+KEYWORDS: clojure macros beginner tutorial lisp future

I was shying back from Lisp macros for a long time. It seemed complicated and all the explanations I found were just like that. Why would I even want to use a macro?

At some point I got to a use case where I thought a macro could do the job (even though I was not totally sure what a macro was :).

I had a working function that looked like this:

#+BEGIN_SRC clojure
(defn scrape-chnrs [chnrs]
  (let [count (count chnrs) ; total number of items in `chnrs'
        cursor (atom 0)]    ; counter, init with 0
    (doseq [chnr chnrs]     ; loop over `chnrs'
      (write-xml chnr)      ; do something with current `chnr'
      (swap! cursor inc)    ; increase the counter
      (info (format "%s/%s" @cursor count))))) ; log the current progress, e.g. 433/1000, then 434/1000 etc.
#+END_SRC

I wanted to let that function run in the background. In Clojure you can create a =future= for that:

#+BEGIN_SRC clojure :results code
(future (scrape-chnrs ["1" "2" "3" "4"]))
#+END_SRC

#+BEGIN_SRC clojure
#<Future@5f6cb7b7: :pending>
#+END_SRC

So yeah, the thing was in the background now but I couldn't check the status or cancel it because it had no name. I could give it a name like that:

#+BEGIN_SRC clojure
(def myfuture (future (scrape-chnrs ["1" "2" "3" "4"])))
#+END_SRC

But I thought, "Hey! I would like to dynamically specify the name!". I wanted to be able to use:

#+BEGIN_SRC clojure
(scrape-job myfuture ["1" "2" "3" "4"])
#+END_SRC

which would run and define a future =myfuture=. The macro I wrote is this:

#+BEGIN_SRC clojure
(defmacro scrape-job [jobname chnrs]
  `(def ~jobname (future (scrape-chnrs ~chnrs))))
#+END_SRC

What happens now when this macro is run? It as kind of a two-pass thing. In the first pass the macro is expanded, meaning that (we simulate this first pass with =macroexpand=)

#+BEGIN_SRC clojure :results raw
(macroexpand '(scrape-job myfuture ["1" "2" "3" "4"]))
#+END_SRC

is transformed into:

#+BEGIN_SRC clojure
(def myfuture (clojure.core/future (zefix-clj.scrape/scrape-chnrs ["1" "2" "3" "4"])))
#+END_SRC

In this case I had "quoted" the expression =(def ~jobname (future (scrape-chnrs ~chnrs)))= with a backtick, which means that in the first pass this expressions is not evaluated but for elements prefixed with =~=. In this case these are =jobname= which was passed in as =myfuture= and the numbers.

Now the same macro can also be written without backtick notation:

#+BEGIN_SRC clojure
(defmacro scrape-job [jobname chnrs]
  (list 'def jobname (list 'future (list 'scrape-chnrs chnrs))))
#+END_SRC

With the =list= notation we quote what we *don't want* to evaluate. The result of =macroexpand= is similar:

#+BEGIN_SRC clojure
(def myfuture (future (scrape-chnrs ["1" "2" "3" "4"])))
#+END_SRC

The only difference is that there are no namespaces added. This is because with the backtick notation Clojure adds namespaces to symbols. If you use ='= the text is inserted as is. To avoid namespace issues you would add namespaces manually in this case.

We can see that a macro is like a code-generator. In the first pass our "template" is processed, and in the second pass the produced code is evaluated. Pretty simple, right?

Let's go over it again using a simple example from the Clojure core:

#+BEGIN_SRC clojure
(defmacro when
  "Evaluates test. If logical true, evaluates body in an implicit do."
  {:added "1.0"}
  [test & body]
  (list 'if test (cons 'do body)))
#+END_SRC

=when= uses =if= as a building block. It just does not use the =else= part of =if=. As we can see, this macro uses the =list= approach. Let's see how =when= expands:

#+BEGIN_SRC clojure :results raw
(macroexpand '(when "trouble" (str 1 '(2 3 4))))
#+END_SRC

#+BEGIN_SRC clojure
(if "trouble" (do (str 1 (quote (2 3 4)))))
#+END_SRC

Everything that follows the first argument is passed on as a list =body=. =body= is then prefixed with =do= (using =cons=) so that all the forms are contained in one expression which is passed as the second argument to =if=.

Boring. On to something more interesting, the =->= macro:

#+BEGIN_SRC clojure
(defmacro ->
  "Threads the expr through the forms. Inserts x as the
  second item in the first form, making a list of it if it is not a
  list already. If there are more forms, inserts the first form as the
  second item in second form, etc."
  {:added "1.0"}
  ([x] x)
  ([x form] (if (seq? form)
              (with-meta `(~(first form) ~x ~@(next form)) (meta form))
              (list form x)))
  ([x form & more] `(-> (-> ~x ~form) ~@more)))
#+END_SRC

This is the famous threading macro. This macro uses arity overloading (see http://clojure.org/functional_programming). If just one argument is provided it returns that. If two arguments are provided...

#+BEGIN_SRC clojure :results raw
(macroexpand '(-> 1 (+ 5 3)))
#+END_SRC

it checks if the form is a sequence, which is true in this case, so it takes the result from =x= and inserts that as the first argument to =+=:

#+BEGIN_SRC clojure
(+ 1 5 3)
#+END_SRC

If the =form= is not a seq it makes a list of =form=  and =x=:

#+BEGIN_SRC clojure :results raw
(macroexpand '(-> 1 inc))
#+END_SRC

#+BEGIN_SRC clojure
(inc 1)
#+END_SRC

Now if we add another expression:

#+BEGIN_SRC clojure :results raw
(macroexpand-1 '(-> 1 (+ 5 3) (- 9)))
#+END_SRC

#+BEGIN_SRC clojure
(clojure.core/-> (clojure.core/-> 1 (+ 5 3)) (- 9))
#+END_SRC

We used =macroexpand-1= here because =macroexpand= calls =macroexpand-1= repeatedly and we would miss the first expansion. The second expansion gives us:

#+BEGIN_SRC clojure
(- (clojure.core/-> 1 (+ 5 3)) 9)
#+END_SRC

To expand everything we need to use =macroexpand-all= from =clojure.walk=:

#+BEGIN_SRC clojure :results raw
(clojure.walk/macroexpand-all '(-> 1 (+ 5 3) (- 9)))
#+END_SRC

#+BEGIN_SRC clojure
(- (+ 1 5 3) 9)
#+END_SRC

So the result is =0=. You can see that complicated stuff in Lisp is building on simple macro operations.
